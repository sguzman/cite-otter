set shell := ["bash", "-uc"]

default:
	@just --list


tokei:
	tokei --exclude vendor --exclude tmp --files --sort lines

llvm-cov:
	cargo llvm-cov

llvm-lines:
	cargo llvm-lines

# Context
context:
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension rs . > ~/Downloads/all.txt

context-by-file:
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension rs . > ~/Downloads/rs.txt
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension md . > ~/Downloads/md.txt
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension toml . > ~/Downloads/toml.txt
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension yaml --extension yml . > ~/Downloads/yaml.txt
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension txt . > ~/Downloads/txt.txt
	files-to-prompt --ignore tmp --ignore target/ --ignore .git/ --markdown --line-numbers --extension sh --extension bash . > ~/Downloads/sh.txt

# Release
major:
	cargo release major --verbose --execute --no-publish --no-push --no-confirm
	git push
	git push --tags

minor:
	cargo release minor --verbose --execute --no-publish --no-push --no-confirm
	git push
	git push --tags

patch:
	cargo release patch --verbose --execute --no-publish --no-push --no-confirm
	git push
	git push --tags

# Copy over template files
template dir:
	cp -riv {{dir}}/docs .
	cp -iv {{dir}}/*.toml .
	cp -iv {{dir}}/about.hbs .
	cp -iv {{dir}}/biome.json .
	cp -iv {{dir}}/.gitignore .
	cp -iv {{dir}}/LICENSE .

# Build
build:
	cargo build 

# Format (write/fix)
fmt:
	cargo fmt
	taplo fmt
	biome check --write .
	rumdl fmt .

# Validate (configs)
validate:
	taplo validate

# Spellcheck / Links
typos:
	typos --config typos.toml

links:
	lychee --config lychee.toml .

# Lint
clippy:
	cargo clippy --all-targets -- -D warnings

biome:
	biome check .

rumdl:
	rumdl check .

# Test
test:
	cargo test --all-features

test-fast:
	cargo test --all-features --no-run
	cargo test --test reference_parser
	cargo test --test reference_format
	cargo test --test reference_training
	cargo test --test reference_ruby_format_parity
	cargo test --bin summarize_hyperfine

# Docs
doc:
	cargo doc --no-deps

# Data
sync-normalization:
	scripts/sync_normalization_any.sh

refresh-format-fixtures:
	CITE_OTTER_CORE_LIMIT=200 cargo run --quiet --bin generate_format_fixtures

refresh-fixtures:
	scripts/sync_normalization_any.sh
	CITE_OTTER_CORE_LIMIT=200 cargo run --quiet --bin generate_format_fixtures
	cargo test

refresh-fixtures-fast:
	scripts/sync_normalization_any.sh
	CITE_OTTER_CORE_LIMIT=200 cargo run --quiet --bin generate_format_fixtures

compare-ruby-format:
	bash scripts/compare_ruby_format.sh

bench-ruby-parity:
	ENABLE_TRAINING_BENCHMARKS=0 scripts/benchmark_ruby_parity.sh

bench-ruby-parity-full:
	ENABLE_TRAINING_BENCHMARKS=1 scripts/benchmark_ruby_parity.sh

bench-rust-baseline:
	scripts/benchmark_rust_baseline.sh

summarize-hyperfine report:
	cargo run --quiet --bin summarize_hyperfine -- {{report}}

# Security (Rust)
audit:
	cargo audit

deny:
	cargo deny check

# Coverage (Rust)
# (Requires cargo-llvm-cov installed)
coverage:
	cargo llvm-cov --all-features --lcov --output-path target/coverage.lcov

coverage-html:
	cargo llvm-cov --all-features --open

# CI-style checks (no writes)
fmt-check:
	cargo fmt --check
	taplo fmt --check
	biome check .
	rumdl check .

# "CI local" = what you'd run before pushing
ci: fmt-check validate typos links biome clippy test doc build

# "Fast" = default local checks without long parity/benchmark steps
verify-fast: fmt-check validate test-fast build

# "Full" = full CI + Ruby fixture parity (still no hyperfine by default)
verify-full: ci compare-ruby-format

# "Extended" = full verification plus long-running hyperfine suites
verify-full-with-benchmarks: verify-full bench-rust-baseline bench-ruby-parity-full

# "All" = auto-fix + run the suite
all: fmt validate typos links biome clippy test doc build
